import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'xyz.malkki.neostumbler'
    compileSdk 34

    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "xyz.malkki.neostumbler"
        minSdk 29
        targetSdk 34
        versionCode 5
        versionName "1.1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
            arg("room.incremental", "true")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".dev"
        }

        release {
            signingConfig signingConfigs.release

            debuggable false

            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'gson.pro', 'altbeacon.pro'
        }
    }

    buildFeatures {
        flavorDimensions += "version"
    }

    productFlavors {
        fdroid {
            dimension "version"

            applicationId = defaultConfig.applicationId + ".fdroid"
        }
        full {
            dimension "version"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}



tasks.register('printVersionName') {
    print project.android.defaultConfig.versionName
}

tasks.withType(KotlinCompilationTask).configureEach {
    compilerOptions {
        freeCompilerArgs.add("-opt-in=kotlin.time.ExperimentalTime")
        freeCompilerArgs.add("-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi")
        freeCompilerArgs.add("-opt-in=androidx.compose.material3.ExperimentalMaterial3Api")
        freeCompilerArgs.add("-opt-in=kotlinx.coroutines.FlowPreview")
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0"

    implementation platform('androidx.compose:compose-bom:2024.02.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.runtime:runtime-livedata"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation platform('org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.8.0')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8'
    fullImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services'

    fullImplementation 'com.google.android.gms:play-services-location:21.2.0'

    implementation "androidx.datastore:datastore-preferences:1.0.0"

    def room_version = "2.6.1"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    def work_version = "2.9.0"

    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation platform("com.squareup.okhttp3:okhttp-bom:4.12.0")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"
    testImplementation "com.squareup.okhttp3:mockwebserver"

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'org.altbeacon:android-beacon-library:2.20.3'

    implementation 'org.osmdroid:osmdroid-android:6.1.18'

    implementation 'org.apache.commons:commons-csv:1.10.0'
    
    implementation "com.patrykandpatrick.vico:compose:1.13.1"
    implementation "com.patrykandpatrick.vico:compose-m3:1.14.0"

    testImplementation "org.mockito.kotlin:mockito-kotlin:5.2.1"
}
